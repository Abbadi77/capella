--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _77dBkJ6NEeWcU-ojRg-Rxw,_LFiS0FpOEeaZtZLaGPKybA
Element-Name: CheckAFMFilePresenceAfterMigration
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _g6DIAFpOEeaZtZLaGPKybA
Runtime-Version: 2.0.2.201511100828
Save-Time: 8/4/16 5:51 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// Check the model obsolescence (a popup with an error message ) 
get-view "Capella Project Explorer" | get-tree | select "previous_version/previous_version.aird"
exec-with-options {
    get-view "Capella Project Explorer" | get-tree | get-menu "Open Session" | click
    get-window "Progress Information" | get-window "Problem Occurred" | get-control Any -index 1 
        | get-property "getChildren().Control[1].getText()" | contains "Wrong model version detected" | verify-true
    get-window "Progress Information" | get-window "Problem Occurred" | get-button OK | click
} -allowStatusDialog

// Migrate the model
get-view "Capella Project Explorer" | get-tree | select "previous_version" 
    | get-menu "Migration/Migrate Project toward current version" | click
with [get-window "Model Migration"] {
    get-button "Backup models before the migration" | uncheck
    get-button OK | click
}

// Unhide AFM resources
get-view "Capella Project Explorer" | get-tree | select "previous_version/previous_version.aird"
get-button "View Menu" | click
get-view "Capella Project Explorer" | get-menu "Customize View..." | click
with [get-window "Available Customizations"] {
    get-tab-folder | get-tab-item Filters | click
    get-table | get-item "\\*.afm resources" | uncheck
    get-button OK | click
}

// Check the presence of .afm resource
get-view "Capella Project Explorer" | get-tree | get-item "previous_version/previous_version.afm" 
    | get-property caption | equals "previous_version.afm" | verify-true
get-view "Capella Project Explorer" | get-tree | select "previous_version/previous_version.aird" 
    | get-menu "Open Session" | click
    
// Check that the session is correctly opened (Open a diagram and check it has 3 children)
get-view "Capella Project Explorer" | get-tree 
    | select "previous_version/previous_version.aird/previous_version/Logical Architecture/Logical System/\\[LAB\\] Logical System" 
    | double-click
get-editor "[LAB] Logical System" | get-diagram -index 1 | get-edit-part -name "[LAB] Logical System" 
    | get-property childrenCount | equals 3 | verify-true
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
